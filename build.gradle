buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: "forge"

// define the properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

group = "stuuupiiid.guncus"
version = config.mod_version
archivesBaseName = "GunCustomization" // http://maven.apache.org/guides/mini/guide-naming-conventions.html

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    assetDir = "run/assets"

    // replacing
    replace "@version@", project.version
    replaceIn "GunCus.java"

    replaceIn "lib/Reference.java"
    if (project.hasProperty("guncus_signature"))
        replace "@FINGERPRINT@", project.guncus_signature
}

version = "${config.minecraft_version}-${config.mod_version}.${System.getenv("BUILD_NUMBER") ?: 16}"

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'version.properties'

        // replaces
        expand 'mod_version': project.version, 'minecraft_version': project.config.minecraft_version
    }

    // copy everything else, thats we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'version.properties'
    }
}

// BEYOND THIS POINT.. 
// IS STUFF THATS FOR RELEASING...

// verify the properties exist.. or initialize.
if (!project.hasProperty("keystore_location")) // keystore stuff
    ext.keystore_location = "."

if (!project.hasProperty("guncus_keystore_alias")) // keystore stuff
    ext.guncus_keystore_alias = ""

if (!project.hasProperty("keystore_password")) // keystore stuff
    ext.keystore_password = ""

if (!project.hasProperty("guncus_release_loc")) // release loc
    ext.guncus_release_loc = "."
else
    ext.guncus_release_loc = guncus_release_loc.replace('{MC}', minecraft.version).replace('{MODVER}', config.mod_version).replace('{BUILD}', "${System.getenv().BUILD_NUMBER}")

task signJar(dependsOn: "reobf") {
    inputs.file jar.getArchivePath()
    inputs.file keystore_location
    inputs.property "guncus_keystore_alias", guncus_keystore_alias
    inputs.property "keystore_password", keystore_password
    outputs.file jar.getArchivePath()

    // only sign if the keystore exists
    onlyIf {
        return keystore_location != "."
    }

    // the actual action.. sign the jar.
    doLast {
        ant.signjar(
                destDir: jar.destinationDir,
                jar: jar.getArchivePath(),
                keystore: keystore_location,
                alias: guncus_keystore_alias,
                storepass: keystore_password
        )
    }
}

task release(type: Copy) {
    dependsOn "signJar"

    from project.tasks.jar.destinationDir
    into project.file(project.guncus_release_loc)

    eachFile { file ->
        logger.info "copying ${file}"
    }

    // only if the release location isn't empty.
    onlyIf {
        return project.guncus_release_loc != "."
    }
}
